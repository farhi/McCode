/*******************************************************************************
*         McXtrace instrument definition URL=http://www.mcxtrace.org
*
* Instrument: Test_SX
*
* %Identification
* Written by: E. Farhi
* Date: Sept 26th 2019
* Origin: Synchrotron Soleil
* Version: 0.2
* %INSTRUMENT_SITE: Tests_samples
*
* Unit-test instrument for the Single_crystal sample component.
*
* Simply a model source illuminating a SX sample.
* The sample itself is a Mo bulk crystal.
* The idea is to compare the fluorescence and diffraction patterns:
* - index=1: use Single_crystal
* - index=2: use FluoCrystal
* - index=3: use Fluorescence+Single_crystal in a GROUP
*
* %Example: index=1 Detector: psd4pi_I=1.18843e-13
* %Example: index=2 Detector: psd4pi_I=6.89931e-12
* %Example: index=3 Detector: psd4pi_I=4.4547e-13
*
* %Parameters
* reflections: [str] List of powder reflections, LAU/CIF format.
* index:       [1]   Index of the sample component to use. 1=Single_crystal, 2=FluoCrystal, 3=Fluorescence+Single_crystal in a GROUP
*
* %End
*******************************************************************************/

DEFINE INSTRUMENT Test_SX(string reflections="Mo.lau", index=1)

USERVARS %{
  int Stype;
%}

TRACE
COMPONENT src = Source_flat(
    yheight = 1e-3, xwidth = 1e-3, dist = 10, focus_xw = 1e-3,
    focus_yh = 1e-3, E0 = 7, dE = 6.9)
AT (0, 0, 0) ABSOLUTE

COMPONENT sample_pos=Arm()
AT (0, 0, 10) RELATIVE PREVIOUS

COMPONENT sample = Single_crystal(reflections=reflections, material_datafile="NULL", 
    radius = .5e-4, yheight = 1e-3, mosaic=1)
    WHEN(index==1)
AT (0, 0, 0) RELATIVE sample_pos
EXTEND %{
  if(!SCATTERED) ABSORB;
  else Stype=DIFFRACTION;
%}

COMPONENT sampleF = FluoCrystal(material=reflections,
    radius = .5e-4, yheight = 1e-3, sx_mosaic=5)
    WHEN(index==2)
AT (0, 0, 0) RELATIVE sample_pos
EXTEND %{
  if(!SCATTERED) ABSORB;
  else Stype=type;
%}

COMPONENT FluoG = Fluorescence(
  radius = .5e-4, yheight = 1e-3, material=reflections)
WHEN (index == 3)
AT (0, 0, 0) RELATIVE sample_pos
GROUP FluSX
EXTEND %{
  if (SCATTERED) Stype = type;
%}

COMPONENT SX = COPY(sample)(sigma_inc=-1)
WHEN (index == 3)
AT (0, 0, 0) RELATIVE sample_pos
GROUP FluSX
EXTEND %{
  if (SCATTERED) Stype=DIFFRACTION;
  else ABSORB;
%}

COMPONENT psd4pi = PSD_monitor_4PI(
    nx = 180, ny = 180, filename = "psd4pi", radius = 0.1, restore_xray = 1)
AT (0, 0, 0) RELATIVE sample

COMPONENT psd_Diff = PSD_monitor_4PI(
    nx = 180, ny = 180, filename = "psd4pi_diff", radius = 0.1, restore_xray = 1)
WHEN (Stype == DIFFRACTION)
AT (0, 0, 0) RELATIVE sample

END
