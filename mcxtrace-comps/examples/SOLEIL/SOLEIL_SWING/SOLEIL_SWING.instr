/*******************************************************************************
* Instrument: SOLEIL_SWING
*
* %Identification
* Written by: E. Farhi, S. Bac
* Date: 2025
* Origin: SOLEIL
* Version: 0.1
* %INSTRUMENT_SITE: SOLEIL
*
* A simple model for the SWING beam-line at SOLEIL (small angle scattering, SAXS).
*
* %Description
* The SWING beamline targets soft condensed matter, conformation of
* macro-molecules in solution (BioSAXS) and material sciences. Our experimental
* set up allows simultaneous small-angle X-ray scattering (SAXS) and wide-angle
* X-ray scattering measurements (WAXS) in the 5-16 keV energy range. Anomalous
* scattering experiments can also be performed. A very large variety of types of
* samples can be studied, e.g., solutions, gels, amorphous solids, crystallized
* solids, thanks to the diversity of the proposed sample environments.
*
* This model may use any PDB file as sample, or an ideal sphere colloid for testing.
*
* Position | Element
* ---------|--------------------------------------------------------------------
* 0        | U20 undulator
* 11.7     | Diaphragm providing HxV=1x0.5 mm beam
* 20       | Si(111) DCM, 40x40x10 mm^3 E0=5-16 keV
* 22.5     | Focusing KB (HFM + VFM)
* 31       | Linear H-CRL (f=81 cm): 31 m
* 32       | Sample location
* 32.5-39  | Detector 162.5 x 155.2 mm
*
* Example: E0=13 Detector: Eiger4M_I=7.5752e+06
*
* %Parameters
* E0:             [keV]  Nominal energy at the Wiggler.
* dE:             [keV]  Energy half-bandwidth at the Wiggler
* dcm_theta:      [deg]  Rotation angle of the DCM. 0=set from energy E0
* mirror_grazing_angle: [deg] Tilt angle of the mirrors.
* hfm_radius:       [m]  Horizontally focusing mirror radius.
* vfm_radius:       [m]  Vertically focusing mirror radius.
* sample:         [str]  Sample given as a PDB file, or NULL for a 100A dilute Sphere model.
* sample_det:       [m]  Sample to detector distance in m.
*
* %Link
* https://www.synchrotron-soleil.fr/en/beamlines/swing
*
* %End
*******************************************************************************/
DEFINE INSTRUMENT SOLEIL_SWING(E0=13, dE=0.1, dcm_theta=0,
  hfm_radius=495, vfm_radius=859, mirror_grazing_angle=4e-3,
  string sample="6lyz.pdb", sample_det=2)

DECLARE
%{
  double dcm_gap;
  double Lambda;
  double Ki;
  int    flag_sample_sphere;
%}

USERVARS
%{
  int    flag_hfm;
  int    flag_vfm;
%}

INITIALIZE
%{
  double DM= 5.4909;     // Si d-spacing for the Monochromator
  double d = DM/sqrt(3); // <111> reflection |<111>|=3
  dcm_gap  = 0.02;       // gap between the 2 monochromator crystals
   
  if (!dcm_theta && E0) {
    // n.lambda = 2 d sin(dcm_theta) = 2*PI/E2K / E0 with n=ORDER=1
    double sin_theta = 2*PI/E2K/E0 / 2 / d;
    if (fabs(sin_theta) < 1)
      dcm_theta = asin(sin_theta)*RAD2DEG;
  } else if (dcm_theta && !E0)
    E0 = 2*PI/E2K / (2*d*sin(dcm_theta*DEG2RAD));
  if (!dcm_theta || !E0 || dE <=0)
    exit(fprintf(stderr, "%s: ERROR: Monochromator can not reflect E0=%g +/- %g [keV]. Aborting.\n", NAME_INSTRUMENT, E0, dE));
  Ki     = E0*E2K;
  Lambda = 2*PI/Ki;
  MPI_MASTER(
    printf("%s: E0=%g [keV] Ki=%g [1/Angs] Lambda=%g [Angs] Monochromator dcm_theta=%g [deg]\n", 
      NAME_INSTRUMENT, E0, dcm_theta, Ki, Lambda);
    if (!strlen(sample) || !strcmp(sample,"0") || !strcmp(sample,"NULL")) {
      flag_sample_sphere=1;
      printf("%s: Using ideal sample %s\n", NAME_INSTRUMENT, "Sphere colloid");
      strcpy(sample, "6lyz.pdb"); // avoid stop at init of SAXSPDBFast
    } else {
      flag_sample_sphere=0;
      printf("%s: Using PDB sample %s\n", NAME_INSTRUMENT, sample);
    }
  );
  if (fabs(dE/E0)>0.1) dE = 3e-2*E0;
%}

TRACE

COMPONENT origin = Progress_bar()
AT (0, 0, 0) RELATIVE ABSOLUTE

// the photon source -----------------------------------------------------------
COMPONENT Source_U20 = Undulator(
    E0 = E0,
    dE = dE,
    Ee = 2.75,
    Ie = 0.5,
    K = 5,
    sigex = 388e-6,
    sigey = 8.1e-6,
    sigepx = 14.5e-6,
    sigepy = 4.61e-6)
AT (0,0,0) RELATIVE origin

COMPONENT mon_src_xy = Monitor_nD(
  options="x y", xwidth=2e-3, yheight=2e-3, bins=128)
AT (0,0,11.7) RELATIVE Source_U20

COMPONENT mon_src_e = Monitor_nD(options="energy", xwidth=2e-3, yheight=2e-3, bins=128,
  min=E0-dE*1.1, max=E0+dE*1.1)
AT (0,0,0) RELATIVE PREVIOUS

// Diaphragm
COMPONENT slit = Slit(xwidth=1e-3, yheight=0.5e-3)
AT (0,0,0.1) RELATIVE PREVIOUS

COMPONENT slit_mon_xy = COPY(mon_src_xy)
AT (0,0,0) RELATIVE PREVIOUS

// The double monochromator ----------------------------------------------------
COMPONENT DCM_location = Arm()
AT (0,0,20) RELATIVE Source_U20

COMPONENT dcm_xtal0 = Bragg_crystal(
    length=0.04,      width=0.04, 
    h=1, k=1, l=1, material="Si.txt", V=160.1826)
AT(0,0,0)          RELATIVE PREVIOUS
ROTATED (-dcm_theta,0,0) RELATIVE PREVIOUS
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT dcm0      = Arm()
AT(0,0,0)          RELATIVE PREVIOUS
ROTATED (-dcm_theta,0,0) RELATIVE PREVIOUS

COMPONENT dcm_xtal1 = COPY(dcm_xtal0)
AT(0,dcm_gap, dcm_theta ? dcm_gap/tan(dcm_theta*DEG2RAD) : 0)    RELATIVE dcm_xtal0
ROTATED (dcm_theta,0,0)  RELATIVE PREVIOUS
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT dcm1      = Arm()
AT(0,0,0)          RELATIVE PREVIOUS
ROTATED (dcm_theta,0,0)  RELATIVE PREVIOUS 

COMPONENT mon_dcm_e = COPY(mon_src_e)
AT (0,0,0.5) RELATIVE PREVIOUS

COMPONENT mon_dcm_xy = COPY(mon_src_xy)
AT (0,0,0) RELATIVE PREVIOUS

// KB pair ---------------------------------------------------------------------
// could use Mirror_elliptic
// incidence angle 4 mrad

// KB 1st mirror (XZ plane) at 1.6m from sample
// must be shifted by -2 mm  on Y else beam flies underneath the mirror.
COMPONENT vfm_location = COPY(mon_src_xy)
AT (0,0,22.5-20.5) RELATIVE mon_dcm_xy

COMPONENT vfm_in_xz_plane = Arm()
AT (0,0,0) RELATIVE vfm_location
ROTATED(0,0,-90) RELATIVE vfm_location // YZ -> XZ

COMPONENT vfm = Mirror_curved(
  coating="Rh.txt",
  length=450e-3,
  width=0.1,
  radius=vfm_radius)
AT (0,0,0) RELATIVE vfm_location
ROTATED(0,-mirror_grazing_angle,0) RELATIVE vfm_in_xz_plane
EXTEND %{
  flag_vfm=SCATTERED;
%}

COMPONENT vfm_takeoff = COPY(vfm_location)
AT (0,0,0) RELATIVE vfm_in_xz_plane
ROTATED (-2*mirror_grazing_angle, 0, 0) RELATIVE vfm_location

// KB 2nd mirror (YZ) at 1m from sample, shift height=4.8 mm 
COMPONENT hfm_location = COPY(vfm_location)
AT (0,0,34.5-33.9) RELATIVE vfm_takeoff

// 859 m tangential; sagital > 1 km, in YZ plane
COMPONENT hfm = Mirror_curved(
  coating="Rh.txt",
  length=450e-3,
  width=0.1,
  radius=hfm_radius)
AT (0,0,0) RELATIVE hfm_location
ROTATED (0,-mirror_grazing_angle,0) RELATIVE hfm_location
EXTEND %{
  flag_hfm=SCATTERED;
%}

COMPONENT hfm_takeoff = COPY(vfm_location)
AT (0,0,0) RELATIVE hfm_location
ROTATED (0,-2*mirror_grazing_angle,0) RELATIVE hfm_location
EXTEND %{
  if (!flag_hfm || !flag_vfm) ABSORB;
%}


// The sample area -------------------------------------------------------------
// align surface so that the beam hits its centre (shift along Y axis)
SPLIT 10 COMPONENT sample_stage = COPY(mon_src_xy)(xwidth=0.01, yheight=0.01,
  restore_xray=1)
AT (1e-3,-1.54e-3,32-22.5) RELATIVE hfm_takeoff

COMPONENT sample_test = Saxs_spheres(
    R = 100, Phi = 0.1, Delta_rho = 1.6,
    yheight = 1e-3, radius = 0.25e-3, focus_xw = 1,
    focus_yh = 1, sphere_mtrl="Be.txt")
WHEN( flag_sample_sphere )
AT (0,0,0) RELATIVE sample_stage
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT sample = SAXSPDBFast(
  xwidth  = 0.01,
  yheight = 0.01,
  zdepth  = 0.01,
  SampleToDetectorDistance = sample_det,
  DetectorRadius = 0.16*1.41, 
  PDBFilepath = sample,
  NumberOfQBins=2000,
  qMax = 0.9)
WHEN( !flag_sample_sphere )
AT (0,0,0) RELATIVE sample_stage
EXTEND %{
  if (!SCATTERED) ABSORB;
%}

COMPONENT sample_out = Arm()
AT (0,0,0)       RELATIVE sample_stage

COMPONENT Eiger4M = Monitor_nD(xwidth=0.5, yheight=0.5, bins=1024,
  options="x y")
AT (0,0,sample_det) RELATIVE sample_stage

COMPONENT QMonitor = SAXSQMonitor(
  RadiusDetector     = 0.22,
  DistanceFromSample = sample_det,
  LambdaMin          = Lambda,
  Lambda0            = Lambda,
  NumberOfBins       = 2000
)
AT (0,0,sample_det) RELATIVE sample_stage


END
